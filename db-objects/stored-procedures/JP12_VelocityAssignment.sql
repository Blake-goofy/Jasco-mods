SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*
 Mod     | Programmer    | Date        | Modification Description
 --------------------------------------------------------------------
 JP12    | Blake Becker  | 01/16/2025  | Created. Ran via sched job in scale: JP11 Velocity Assignment
*/

CREATE OR ALTER PROCEDURE [dbo].[JP12_VelocityAssignment] AS

SET NOCOUNT ON;

DECLARE @JP12_HEAVY_WEIGHT NUMERIC (19,5) = (SELECT TRY_CONVERT(NUMERIC (19,5), SYSTEM_VALUE) FROM SYSTEM_CONFIG_DETAIL WHERE RECORD_TYPE = N'Technical' AND SYS_KEY = N'JP12_HEAVY_WEIGHT');

WITH TOTALS AS (
	SELECT
		TOTAL_LOCATIONS = COUNT(*)
		,FULL_LOCATIONS = SUM(CASE WHEN LOCATION_STS = N'Empty' THEN 0 ELSE 1 END)
		,A = SUM(CASE WHEN LOCATING_ZONE = N'L-A Mover' THEN 1 ELSE 0 END)
		,B = SUM(CASE WHEN LOCATING_ZONE = N'L-B Mover' THEN 1 ELSE 0 END)
		,C = SUM(CASE WHEN LOCATING_ZONE = N'L-C Mover' THEN 1 ELSE 0 END)
		,D = SUM(CASE WHEN LOCATING_ZONE = N'L-D Mover' THEN 1 ELSE 0 END)
		,CAP = SUM(CASE WHEN LOCATION_STS = N'Empty' THEN 0	ELSE 1 END) * 1.0 / COUNT(*) + 0.1
	FROM
		LOCATION L WITH (NOLOCK)
		INNER JOIN GENERIC_CONFIG_DETAIL Z WITH (NOLOCK) ON IDENTIFIER = LOCATING_ZONE AND RECORD_TYPE = N'L-ZONES'
	WHERE
		LOCATION_STS != N'Frozen'
		AND L.ACTIVE = N'Y'
)

,CAPS AS ( -- CAPS are the numbers of locations at which each area will be filled to an even percent: the percent capacity of active locating area
	SELECT
		*
		,A_CAP = A * CAP
		,B_CAP = (A * CAP) + (B * CAP)
		,C_CAP = (A * CAP) + (B * CAP) + (C * CAP)
	FROM
		TOTALS
)

,VISITS_EVAL AS (
	SELECT
		V.ITEM
		,PICK_VISITS = SUM(PICK_VISITS)
		,REPLEN_VISITS = SUM(REPLEN_VISITS)
		,QUARTERS = COUNT(*)
	FROM
		VISITS V
	WHERE 
		(
			-- Include records within the rolling 5-quarter window
			(V.YEAR = YEAR(GETDATE())) -- Current year, up to current quarter
			OR
			(V.YEAR = YEAR(GETDATE()) - 1 AND V.QUARTER >= DATEPART(QUARTER, GETDATE() ) ) -- Previous year, valid quarters
		)
	GROUP BY
		V.ITEM
)

,VELOCITY AS (
	SELECT
		I.ITEM
		,NEW =
			CASE
				WHEN ISDATE(ISNULL(I.USER_DEF5, DATEADD(DAY, -70, GETDATE()))) = 1 AND DATEDIFF(DAY, ISNULL(I.USER_DEF5, DATEADD(DAY, -70, GETDATE())), GETDATE()) <= 60 THEN N'Y'
				ELSE N'N'
			END
		,ITEM_CATEGORY1
		,VISITS = ISNULL(PICK_VISITS + (REPLEN_VISITS * 2), 0)
		,PICK_VISITS
		,REPLEN_VISITS
		,LOCS = ISNULL(LOCS, 0) + ISNULL(PALLETS, 0)
		,CUMULATIVE_LOCATIONS = SUM(ISNULL(LOCS, 0) + ISNULL(PALLETS, 0)) OVER (ORDER BY ISNULL(PICK_VISITS + (REPLEN_VISITS * 2), 0) DESC, V.ITEM)
	FROM
		ITEM I WITH (NOLOCK)
		LEFT JOIN VISITS_EVAL V ON V.ITEM = I.ITEM
		LEFT JOIN (
			SELECT ITEM, LOCS = COUNT(DISTINCT LI.LOCATION)
			FROM LOCATION_INVENTORY LI WITH (NOLOCK)
			INNER JOIN LOCATION L ON L.LOCATION = LI.LOCATION
			INNER JOIN GENERIC_CONFIG_DETAIL Z WITH (NOLOCK) ON IDENTIFIER = LOCATING_ZONE AND RECORD_TYPE = N'L-ZONES'
			GROUP BY ITEM) LI ON LI.ITEM = I.ITEM
		LEFT JOIN (
			SELECT RD.ITEM, PALLETS = SUM(FLOOR(RD.OPEN_QTY / PL.CONVERSION_QTY) + (CASE WHEN RD.OPEN_QTY % PL.CONVERSION_QTY > 0 THEN 1 ELSE 0 END))
			FROM RECEIPT_DETAIL RD
			INNER JOIN RECEIPT_HEADER RH ON RD.INTERNAL_RECEIPT_NUM = RH.INTERNAL_RECEIPT_NUM
			INNER JOIN TRAILER_YARD_STATUS T ON T.OBJECT_ID = RH.TRAILER_YARD_STATUS_ID
			INNER JOIN ITEM_UNIT_OF_MEASURE PL ON PL.ITEM = RD.ITEM AND PL.QUANTITY_UM = N'PL'
			WHERE RD.OPEN_QTY > 0
				AND (CHARINDEX(N'Oklahoma City', TRAILER_LOCATION) > 0
				OR CHARINDEX(N'Dallas', TRAILER_LOCATION) > 0)
			GROUP BY RD.ITEM) RD ON RD.ITEM = I.ITEM
	WHERE
		V.ITEM IS NOT NULL
		OR LI.LOCS > 0
		OR RD.PALLETS > 0
)

UPDATE ITEM SET
	ITEM_CATEGORY1 =
		CASE
			WHEN CS.WEIGHT > @JP12_HEAVY_WEIGHT AND CS.CONVERSION_QTY != PL.CONVERSION_QTY AND NEW = N'Y' THEN N'A Mover Heavy'
			WHEN NEW = N'Y' THEN N'A Mover'

			-- When the case weighs more than 30 lbs and not a gaylord and A then 'A Mover Heavy'
			WHEN CS.WEIGHT > @JP12_HEAVY_WEIGHT AND CS.CONVERSION_QTY != PL.CONVERSION_QTY AND ISNULL(CUMULATIVE_LOCATIONS, 0) < A_CAP THEN N'A Mover Heavy'
			WHEN CUMULATIVE_LOCATIONS < A_CAP THEN N'A Mover'

			WHEN CS.WEIGHT > @JP12_HEAVY_WEIGHT AND CS.CONVERSION_QTY != PL.CONVERSION_QTY AND ISNULL(CUMULATIVE_LOCATIONS, 0) < B_CAP THEN N'B Mover Heavy'
			WHEN CUMULATIVE_LOCATIONS < B_CAP THEN N'B Mover'

			WHEN CS.WEIGHT > @JP12_HEAVY_WEIGHT AND CS.CONVERSION_QTY != PL.CONVERSION_QTY AND ISNULL(CUMULATIVE_LOCATIONS, 0) < C_CAP THEN N'C Mover Heavy'
			WHEN CUMULATIVE_LOCATIONS < C_CAP THEN N'C Mover'

			WHEN CS.WEIGHT > @JP12_HEAVY_WEIGHT AND CS.CONVERSION_QTY != PL.CONVERSION_QTY THEN N'D Mover Heavy'
			ELSE N'D Mover'
		END
FROM
	ITEM I
	INNER JOIN VELOCITY V ON I.ITEM = V.ITEM
	LEFT JOIN CAPS ON V.ITEM = V.ITEM
	LEFT JOIN (
	    SELECT X.ITEM, X.QUANTITY_UM, X.X_REF_ITEM, X.USER_DEF2, U.USER_DEF3, U.CONVERSION_QTY, WEIGHT
	    FROM ITEM_CROSS_REFERENCE X WITH(NOLOCK)
	    INNER JOIN ITEM_UNIT_OF_MEASURE U WITH(NOLOCK) ON X.ITEM = U.ITEM AND X.QUANTITY_UM = U.QUANTITY_UM
	    WHERE X.QUANTITY_UM = N'CS'
	) CS ON V.ITEM = CS.ITEM
	LEFT JOIN ITEM_UNIT_OF_MEASURE PL WITH (NOLOCK) ON PL.ITEM = V.ITEM AND PL.QUANTITY_UM = N'PL'

UPDATE RECEIPT_DETAIL SET
	ITEM_CATEGORY1 = I.ITEM_CATEGORY1
FROM
	RECEIPT_DETAIL RD
	INNER JOIN ITEM I ON I.ITEM = RD.ITEM
GO
