SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO


/*
 Mod     | Programmer    | Date        | Modification Description
 --------------------------------------------------------------------
 001     | mrjackson	 | 03/31/2021  | Stored Procedure for exit point:Locate - Before
 JP9     | Blake Becker	 | 01/13/2026  | Added QC logic.
*/

CREATE OR ALTER PROCEDURE [dbo].[EXP_LocateBefore] (
    @SESSIONVALUE xml,
    @INTERNALRECEIPTNUM nvarchar(max),
    @INTERNALRECEIPTCONTAINERNUM nvarchar(max)
)
AS

SET NOCOUNT ON;

DECLARE @INTERNAL_RECEIPT_LINE_NUM NUMERIC (9,0)
DECLARE @ITEM NVARCHAR (100)
DECLARE @PURCHASE_ORDER_ID NVARCHAR (25)
DECLARE @ITEM_CATEGORY7 NVARCHAR (100)
DECLARE @ITEM_CAT NVARCHAR (100)
DECLARE @PROCESS_ITEM NVARCHAR (100)
DECLARE @PROCESS_RECEIPT NVARCHAR (50)
DECLARE @PROCESS_LINE NUMERIC (9,0)
DECLARE @QC_REQUIRED_QTY FLOAT
DECLARE @QC_CHECKED_QTY INT
DECLARE @QC_NEW_REQUIRED_QTY INT
DECLARE @CS_QTY FLOAT
DECLARE @OPEN_QTY INT
DECLARE @TOTAL_QTY INT
DECLARE @PL_QTY INT
DECLARE @INTERNAL_RECEIPT_NUM NUMERIC (9, 0)
DECLARE @REMOVE_QC TABLE (
	INTERNAL_RECEIPT_LINE_NUM NUMERIC (9,0)
	,RECEIPT_ID NVARCHAR (50)
	,ITEM NVARCHAR (100)
)

--Declaring the Variables for Process History                   
DECLARE @stProcess NVARCHAR(50)
	,@stAction NVARCHAR(50)
	,@stIdentifier1 NVARCHAR(200)
	,@stIdentifier2 NVARCHAR(200)
	,@stIdentifier3 NVARCHAR(200)
	,@stIdentifier4 NVARCHAR(200)
	,@stMessage NVARCHAR(500)
	,@stProcessStamp NVARCHAR(100)
	,@stUserName NVARCHAR(30)
	,@stWarehouse NVARCHAR(25)
	,@cProcHistActive NVARCHAR(2) = NULL; -- future use

begin; --0.0 add PO info to Transaction History

    update th set th.USER_DEF6 = rd.PURCHASE_ORDER_ID, th.USER_DEF7 = rd.PURCHASE_ORDER_LINE_NUMBER

    from        TRANSACTION_HISTORY th
                    inner join RECEIPT_CONTAINER rc on rc.INTERNAL_REC_CONT_NUM = @INTERNALRECEIPTCONTAINERNUM and rc.CONTAINER_ID = th.CONTAINER_ID
                    inner join RECEIPT_DETAIL rd on rc.INTERNAL_RECEIPT_LINE_NUM = rd.INTERNAL_RECEIPT_LINE_NUM

    where       1 = 1
                and th.USER_DEF6 is null
                and th.USER_DEF7 = 0
                and th.TRANSACTION_TYPE = N'20'

	delete from LOCATION_UNIT_OF_MEASURE where INTERNAL_CONTAINER_NUM = @INTERNALRECEIPTCONTAINERNUM

end; --0.0 add PO info to Transaction History

-- START JP9

-- Collect container information
SELECT
	@INTERNAL_RECEIPT_LINE_NUM = INTERNAL_RECEIPT_LINE_NUM
	,@ITEM = ITEM
	,@INTERNAL_RECEIPT_NUM = INTERNAL_RECEIPT_NUM
FROM
	RECEIPT_CONTAINER
WHERE
	INTERNAL_REC_CONT_NUM = @INTERNALRECEIPTCONTAINERNUM

-- Collect line information
SELECT
	@ITEM_CATEGORY7 = ITEM_CATEGORY7
	,@QC_REQUIRED_QTY = ISNULL(TRY_CONVERT(INT, USER_DEF1), 0)
	,@PURCHASE_ORDER_ID = PURCHASE_ORDER_ID
	,@TOTAL_QTY = TOTAL_QTY
	,@OPEN_QTY = OPEN_QTY
FROM
	RECEIPT_DETAIL
WHERE
	INTERNAL_RECEIPT_LINE_NUM = @INTERNAL_RECEIPT_LINE_NUM

SET @CS_QTY = ISNULL(dbo.JPCI_udf_Item(@ITEM, 'CS'), 1)
SET @PL_QTY = ISNULL(dbo.JPCI_udf_Item(@ITEM, 'PL'), @OPEN_QTY)

SET @QC_REQUIRED_QTY = CEILING(@QC_REQUIRED_QTY / @CS_QTY) * @CS_QTY -- Rounded up to CS qty

SELECT @QC_NEW_REQUIRED_QTY = CEILING(SYSTEM_VALUE / @CS_QTY) * @CS_QTY -- Rounded up to CS qty
FROM SYSTEM_CONFIG_DETAIL WHERE SYS_KEY = N'JP9_QC_NEW_QTY' AND @ITEM_CATEGORY7 LIKE N'%NEW%'

SET @QC_NEW_REQUIRED_QTY = ISNULL(@QC_NEW_REQUIRED_QTY, 0)

-- Handle QC lines
IF @ITEM_CATEGORY7 LIKE N'QC%'
BEGIN
	EXEC JP9_RemoveReceiptQC @INTERNAL_RECEIPT_NUM, N'checked in.'
	
	-- Collect the qty we have checked in that is locating to QCA
	SELECT
		@QC_CHECKED_QTY = SUM(QUANTITY)
	FROM
		RECEIPT_CONTAINER
	WHERE
		INTERNAL_RECEIPT_LINE_NUM = @INTERNAL_RECEIPT_LINE_NUM
		AND LOCATING_RULE = N'QCA'

	SET @QC_CHECKED_QTY = ISNULL(@QC_CHECKED_QTY, 0)
	DECLARE @WAIT BIT
	DECLARE @LAST_CHECK_QTY INT

    SET @LAST_CHECK_QTY = (@TOTAL_QTY - CONVERT(INT, @QC_REQUIRED_QTY)) % @PL_QTY
    SET @LAST_CHECK_QTY = CASE WHEN @LAST_CHECK_QTY = 0 THEN @PL_QTY ELSE @LAST_CHECK_QTY END

	SET @WAIT =
		CASE WHEN
			@QC_REQUIRED_QTY > @QC_NEW_REQUIRED_QTY -- There is non 'NEW' QC qty required
			AND @QC_CHECKED_QTY > @QC_NEW_REQUIRED_QTY + @CS_QTY -- The first non 'NEW' QC case is already checked in.
			AND @OPEN_QTY > @LAST_CHECK_QTY + (@QC_REQUIRED_QTY - @QC_CHECKED_QTY) -- There is more qty than (last check in qty + the amount remaining to QC)
		THEN 1 ELSE 0 END

	DECLARE @CHECK_IN_QTY NUMERIC (28,5)
	SELECT @CHECK_IN_QTY = RC.QUANTITY
	FROM RECEIPT_CONTAINER RC
	WHERE RC.INTERNAL_REC_CONT_NUM = @INTERNALRECEIPTCONTAINERNUM

	-- Compare checked to required to determine if we need to make this one go to QCA
	IF (@QC_CHECKED_QTY < @QC_REQUIRED_QTY AND @WAIT = 0 AND @CHECK_IN_QTY = @CS_QTY)
	BEGIN
		UPDATE RECEIPT_CONTAINER SET
			LOCATING_RULE = N'QCA'
			,USER_STAMP = SUSER_SNAME()
			,PROCESS_STAMP = N'EXP_LocateBefore'
			,DATE_TIME_STAMP = GETUTCDATE()
		FROM
			RECEIPT_CONTAINER RC
		WHERE
			RC.INTERNAL_REC_CONT_NUM = @INTERNALRECEIPTCONTAINERNUM
	END
END
-- END JP9

GO
