SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

/*
 Mod Number | Programmer    | Date       | Modification Description
 --------------------------------------------------------------------
 JP9        | Blake Becker  | 01/13/2026 | Created. 

 This selects report detail fields for SSRS report: ReceiptOverview
*/

CREATE OR ALTER PROCEDURE [dbo].[JP9_RPT_ReceiptOverviewDetails](@INTERNAL_RECEIPT_NUM NUMERIC(9)) AS

SET NOCOUNT ON;

--DECLARE @INTERNAL_RECEIPT_NUM NUMERIC(9) = 80958

DECLARE @TRAILER_ID NVARCHAR (25)
DECLARE @REC_DATE DATETIME

SELECT @TRAILER_ID = TRAILER_ID, @REC_DATE = ISNULL(RECEIPT_DATE, N'0')
FROM RECEIPT_HEADER WHERE INTERNAL_RECEIPT_NUM = @INTERNAL_RECEIPT_NUM

UPDATE RECEIPT_HEADER SET USER_DEF1 = N'Printed' WHERE TRAILER_ID = @TRAILER_ID AND RECEIPT_DATE = @REC_DATE

EXEC JP9_RemoveReceiptQC @INTERNAL_RECEIPT_NUM, N'printed.'

SELECT
	RD.RECEIPT_ID
	,RD.PURCHASE_ORDER_ID
	,RD.ITEM
	,TOTAL_QTY = FORMAT(RD.TOTAL_QTY, N'#,0')
	,IP_QTY = dbo.JPCI_UDF_ITEM(RD.ITEM, N'IP')
	,CS_QTY = dbo.JPCI_UDF_ITEM(RD.ITEM, N'CS')
	,PL_QTY = FORMAT(dbo.JPCI_UDF_ITEM(RD.ITEM, N'PL'), N'#,0')
	,ITEM_DESC = REPLACE(REPLACE(REPLACE(RD.ITEM_DESC, N'  ', N' '), N'  ', N' '), N'  ', N' ')
	,FULL_PL = CONVERT(INT, (RD.TOTAL_QTY - 
		CONVERT(INT, RD.TOTAL_QTY - CEILING(ISNULL(TRY_CONVERT(FLOAT, RD.USER_DEF1), 0) / CS.CONVERSION_QTY) *
		CS.CONVERSION_QTY) % dbo.JPCI_UDF_ITEM(RD.ITEM, N'PL')) / 
		dbo.JPCI_UDF_ITEM(RD.ITEM, N'PL'))
	,PARTIAL_QTY = FORMAT(CONVERT(INT, RD.TOTAL_QTY - CEILING(ISNULL(TRY_CONVERT(FLOAT, RD.USER_DEF1), 0) / CS.CONVERSION_QTY) *
		CS.CONVERSION_QTY) % dbo.JPCI_UDF_ITEM(RD.ITEM, N'PL'), N'#,0')
	,QC_QTY = CEILING(ISNULL(TRY_CONVERT(FLOAT, RD.USER_DEF1), 0) / CS.CONVERSION_QTY) * CS.CONVERSION_QTY
	,QC = CASE WHEN RD.ITEM_CATEGORY7 LIKE N'QC%' THEN N'QC' END
	,BARCODE = RD.ITEM
	,NOTE =
		CASE
			WHEN CS.USER_DEF2 = N'Wrap' THEN NULL
			WHEN CS.USER_DEF2 = N'Band' THEN N'BAND'
			WHEN CS.WEIGHT > 10 AND dbo.JPCI_UDF_ITEM(RD.ITEM, N'PL') / CS.CONVERSION_QTY < 40 THEN N'BAND'
		END
FROM
	RECEIPT_DETAIL RD WITH (NOLOCK)
	LEFT JOIN ITEM_UNIT_OF_MEASURE CS WITH (NOLOCK)
		ON CS.ITEM = RD.ITEM
		AND CS.QUANTITY_UM = N'CS'
WHERE
	RD.INTERNAL_RECEIPT_NUM IN (
		SELECT RH.INTERNAL_RECEIPT_NUM
		FROM RECEIPT_HEADER RH
		WHERE RH.TRAILER_ID = @TRAILER_ID
		AND RH.RECEIPT_DATE = @REC_DATE
	)
ORDER BY
	RD.RECEIPT_ID
	,RD.ERP_ORDER_LINE_NUM
	,RD.INTERNAL_RECEIPT_LINE_NUM

GO
